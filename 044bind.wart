mac (with bindings ... body)
  `((fn ,(map car pair.bindings)
     ,@body)
    ,@(map cadr pair.bindings))

mac (withs bindings ... body)
  if no.bindings
    `(do ,@body)
    `(let ,car.bindings ,cadr.bindings
       (withs ,cddr.bindings ,@body))



mac (proc name params ... body)
  `(def ,name ,params
     ,@body
     nil)

mac (ret var val ... body)
  `(let ,var ,val
     ,@body
     ,var)

mac (rfn name params ... body)
  `(ret ,name nil
      (,name <- (fn ,params
                  ,@body)))

mac (afn params ... body)
  `(rfn self ,params ,@body)

mac (loop vars_vals ... body)
  let bindings (pair vars_vals)
    `((rfn recur ,(map car bindings)
        ,@body)
      ,@(map cadr bindings))

mac (before_calling (f ... params) ... body)
  `(let $old ,f
     (def! (,f ,@params)
       ,@body
       ($old ,@params)))

mac (after_calling (f ... params) ... body)
  `(let $old ,f
     (def! (,f ,@params)
       (ret result ($old ,@params)
         ,@body)))
