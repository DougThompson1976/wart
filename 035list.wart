(defcall cons(l index)
  (elt l index))

(defcall cons(l start end)
  (cut l start end))

(defset cons(l index val)
  (setf (elt l index)
        val))

(defset cons(l start end newlist)
  (setf (subseq l start end)
        newlist))

(redef len(x)
  (if x
    (length (as cons x))
    0))

(def empty(x)
  (no x))

(def testify(x) :case (isa x 'cons)
  [find _ x])

(def wart-last(xs)
  (if (cdr xs)
    (wart-last cdr.xs)
    (car xs)))
(defover last wart-last)

(def consif(x y)
  (if x
    (cons x y)
    y))

(alias copy copy-seq)

(def alist(list-of-pairs)
  (map0 (fn((x y)) (cons x y)) list-of-pairs))

(def dot((a b))
  (cons a b))



(def mappend(f . args)
  (apply 'join (apply 'map0 (as function f) args)))

(def intersperse(x ys ? as 'cons)
  (if ys
    (coerce (cons car.ys
                  (mappend [list x _] cdr.ys))
            as)))

(def split(seq pos)
  (list (cut seq 0 pos) (cut seq pos)))

(def reclist(f xs)
  (and xs
       (or (call f xs)
           (reclist f cdr.xs))))
(def reclist(f s) :case (not listp.s)
  (reclist f (as cons s)))

(def wart-some(f seq)
  (reclist f^car seq))
(defover some wart-some)

(def all(test xs)
  (call ~some ~testify.test xs))

(def mem(test seq)
  (reclist [if (call testify.test car._) _]
           seq))

(defover keep wart-keep)
(def keep(f seq)
  (if seq
    (coerce
      (keep f (as cons seq))
      type.seq)))

(def keep(f seq) :case consp.seq
  (acase car.seq
    f     (cons it (keep f cdr.seq))
    :else (keep f cdr.seq)))

(def wart-rem(test seq)
  (keep ~testify.test seq))
(defover rem wart-rem)

(def trues (f xs)
  (and xs
       (iflet fx (call f car.xs)
          (cons fx (trues f cdr.xs))
          (trues f cdr.xs))))

(redef join args
  (apply 'concatenate (type (car (rem nil args))) args))

(def pos(test s ? idx 0)
  (position-if testify.test s :start idx))
(def rpos(test s ? idx 0)
  (position-if testify.test s :from-end t :start idx))

(def wart-map(f . seqs)
  (if (subtypep (type car.seqs)
                'sequence)
    (apply 'map0 f seqs)
    (apply f seqs)))
(defover map wart-map)
