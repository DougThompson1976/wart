(def parse-url(url)
  (ret ans (obj protocol "http"
                host nil
                port 80
                path "/")
    (awhen (search "://" url)
      (= ans!protocol (downcase (cut url 0 it))
         url (cut url (+ it 3))))
    (aif (pos #\/ url)
       (= ans!host (cut url 0 it)
          ans!path (cut url it))
       (= ans!host url))
    (awhen (pos #\: ans!host)
      (= ans!port (int (cut ans!host 1+.it))
         ans!host (cut ans!host 0 it)))))

(def urldecode(s)
  (tostring
    (forlen i s
      (caselet c s.i
        #\+ (write-char #\space)
        #\% (do (when (< i (- len.s 2))
                  (write-char (as character
                                  (int (cut s 1+.i (+ i 3)) :base 16))))
                (++ i 2))
            (write-char c)))))

(def urlencode(s)
  (tostring
    (each c s
      (write-char #\%)
      (let i int.c
        (if (< i 16) (write-char #\0))
        (pr (str i :base 16))))))
