;; Primitives for binding variables

(defover cl-map map) ; keep old version around since ours is weaker for now
(def wart-map(f . seqs)
  (apply 'map 'list f seqs))
(defover map wart-map)

(mac with(parms . body)
  `(call (fn ,(map 'car pair.parms)
           ,@body)
         ,@(map 'cadr pair.parms)))

(mac wart-let(var val . body)
  `(with (,var ,val) ,@body))
(defover let wart-let)

(mac ret(var val . body)
  `(let ,var ,val ,@body ,var))

(mac withs(parms . body)
  (if (no parms)
      `(do ,@body)
      `(let ,(car parms) ,(cadr parms)
         (withs ,(cddr parms) ,@body))))

(mac alias(f g)
  `(def ,f args
     (apply ',g args)))
