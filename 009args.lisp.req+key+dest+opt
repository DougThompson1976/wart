;; Functions support complex arg lists in wart.

(defmacro def(name params &rest body)
  `(defun ,name ,@(compile-params params body)))

(defmacro mac(name params &rest body)
  (wt-transform `(defmacro ,name ,@(compile-params params body))))

(defmacro fn(params &rest body)
  `(lambda ,@(compile-params params body)))



;; Internals
;; Use let* everywhere here because wart will soon override let

; returns arglist and body suitable for insertion into defun or lambda
; new body understands keyword args
; params format (optionals* ? lazy-optionals* . rest)
; optionals can be destructured
; lazy optionals require keywords if rest is present
(defun compile-params(params body)
  (let* ((args  (uniq))
         (positional  (uniq))
         (keywords   (uniq)))
    `((&rest ,args)
      (let* ((,positional  (positional-args ,args))
             (,keywords   (keyword-args ,args)))
        (let* ,(append
                 (get-required-arg-exprs params positional keywords)
                 (get-optional-arg-exprs params positional keywords))
          ,@body)))))

(defun get-required-arg-exprs(params positional keywords)
  (let ((required-params (required-params params)))
    (map 'list
         (lambda(param)
           (list param
                 `(get-arg ',param ',required-params ,positional ,keywords)))
         (flatten required-params))))

(defun get-optional-arg-exprs(params positional keywords)
  (let ((optional-alist (optional-alist params)))
    (map 'list
         (lambda(param)
           (list param
                 `(fa (get-arg ',param
                               ',(strip-defaults params)
                               ,positional ,keywords)
                      ,(alref param optional-alist))))
         (map 'list 'car optional-alist))))

(defun get-arg(var params positional keywords &key (no-params (lambda(x) nil)))
  (cond
    ((assoc var keywords)  (alref var keywords))
    ((no params)  (values (call no-params positional)
                          'no-arg))
    ((is params var)  positional)
    ((not (consp params))   (values nil 'no-arg))
    ((assoc (car params) keywords)  (get-arg var (cdr params) positional keywords :no-params no-params))
    ((no positional)  (values nil 'no-arg))
    ((is (car params) var)  (car positional))
    (t   (fa (get-arg var (car params) (car positional) keywords :no-params no-params)
             (get-arg var (cdr params) (cdr positional) keywords :no-params no-params)))))



;; Slicing and dicing params

(defun required-params(params)
  (if (and params
           (not (is '? (car params))))
    (cons (car params)
          (required-params (cdr params)))))

(defun keyword-args(args)
  (cond
    ((no args)  ())
    ((not (consp args))  ())
    ((keywordp (car args))  (let* ((param (keyword->symbol (car args))))
                              (cons (cons param (cadr args))
                                    (keyword-args (cddr args)))))
    (t   (keyword-args (cdr args)))))

(defun positional-args(args)
  (cond
    ((no args)  ())
    ((keywordp (car args))  (positional-args (cddr args)))
    (t   (cons (car args)
               (positional-args (cdr args))))))

(defun strip-defaults(params &optional past-?)
  (cond
    ((no params)  ())
    ((rest-param-p params)  params)
    ((is (car params) '?)   (strip-defaults (cdr params) t))
    (t   (cons (car params)
               (strip-defaults
                 (if past-? (cddr params) (cdr params))
                 past-?)))))

(defun optional-alist(params)
  (partition-optional-params (strip-required (strip-rest params))))

(defun partition-optional-params(oparams)
  (cond
    ((not (consp oparams))  ())
    ((not (consp (cdr oparams)))  (list oparams))
    (t   (cons (cons (car oparams)
                     (cadr oparams))
               (partition-optional-params (cddr oparams))))))

(defun strip-rest(params)
  (cond
    ((no params) ())
    ((rest-param-p params)  ())
    (t   (cons (car params)
               (strip-rest (cdr params))))))

(defun strip-required(params)
  (if (consp params)
    (let* ((optargs (member '? params))) ; strip-rest has already run
      (if optargs
        (cdr optargs)
        ()))
    params))

(defun rest-param-p(params)
  (not (consp params)))

; strip the colon
(defun keyword->symbol(k)
  (if (is k ':do)
    'body
    (intern (symbol-name k))))
