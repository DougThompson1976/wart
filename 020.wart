assign cons? _isCons

assign nil? _isNil
assign not nil?
assign no nil?

assign mac (lambda '(name params . body)
  (eval `(assign ,name (lambda ',params
                         (eval ((lambda() ,@body)))))))

mac def(name params . body)
  `(assign ,name (lambda ,params ,@body))

def cadr(x)
  (car (cdr x))

def cddr(x)
  (cdr (cdr x))

; (if) => nil
; (if 34) => 34
; (if 34 35) => 35
; (if 34 35 36) => 35
; (if nil 35 36) => 36
; (if nil 35 36 37) => 37
mac if args
  _if (no args)
    nil
    _if (no (cdr args))
      (car args)
      _if (no (cddr args))
        `(_if ,(car args)
           ,(cadr args))
        `(_if ,(car args)
           ,(cadr args)
           (if ,@(cddr args)))
