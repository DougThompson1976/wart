(defover stdin* *standard-input*)
(defover stdout* *standard-output*)
(synonym flush force-output)

(mac w/infile(var fname ? element-type :default . body)
  `(with-open-file(,var ,fname :element-type ,element-type)
     ,@body))

(mac w/outfile(var fname ? element-type :default . body)
  `(with-open-file(,var ,fname :element-type ,element-type :direction :output :if-exists :supersede)
     ,@body))

(mac w/appendfile(var fname ? element-type :default . body)
  `(with-open-file(,var ,fname :element-type ,element-type :direction :output :if-exists :append)
     ,@body))

(mac w/instring(var s . body)
  `(with-input-from-string(,var ,s)
     ,@body))

(mac w/outstring(var . body)
  `(with-output-to-string(,var)
     ,@body))

(mac w/stdin(port . body)
  `(let stdin* ,port
     ,@body))

(mac w/stdout(port . body)
  `(let stdout* ,port
     ,@body))

(mac fromstring(str . body)
  `(w/instring ,$f ,str
      (w/stdin ,$f
        ,@body)))

(mac tostring body
  `(w/outstring ,$f
     (w/stdout ,$f
       ,@body)))

(mac fromfile(f . body)
  `(w/infile ,$port ,f
     (w/stdin ,$port
       ,@body)))

(mac tofile(f . body)
  `(w/outfile ,$port ,f
     (w/stdout ,$port
       ,@body)))

(mac ontofile(f . body)
  `(w/appendfile ,$port ,f
     (w/stdout ,$port
       ,@body)))
